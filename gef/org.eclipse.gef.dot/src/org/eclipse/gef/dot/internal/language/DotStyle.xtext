/*******************************************************************************
 * Copyright (c) 2016, 2018 itemis AG and others.
 * 
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Tamas Miklossy (itemis AG) - initial API and implementation (bug #461506)
 *     
 *******************************************************************************/
grammar org.eclipse.gef.dot.internal.language.DotStyle hidden(WS)

generate style "http://www.eclipse.org/gef/dot/internal/language/DotStyle"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// XXX: While not documented explicitly, an empty style seems to be valid as well
Style: 
	{Style} (styleItems += StyleItem (',' styleItems+=StyleItem)*)?;

StyleItem:
	name=NAME ('(' args+=NAME (',' args+=NAME)* ')')?;

terminal WS: ' ' | '\t' | '\r' | '\n' ;

terminal NAME:
	(!(WS | '(' | ')' | ',' ))*;

enum NodeStyle:
	bold | dashed | diagonals | dotted | filled | invis | radial | rounded | solid | striped | wedged;

enum EdgeStyle:
	bold | dashed | dotted | invis | solid | tapered;
	
enum ClusterStyle:
	bold | dashed | dotted | filled | invis | radial | rounded | solid |striped;